plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.nbaztec.coveralls-jacoco' version '1.2.14'
    id 'maven-publish'
    id 'signing'
}

group 'com.burdinov'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation('org.assertj:assertj-core:3.22.0')
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}


//Publishing

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            setUrl("https://oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = System.getenv("OSSRH_USER") ?: "Unknown user"
                password = System.getenv("OSSRH_PASS") ?: "Unknown password"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = 'com.burdinov'
                name = 'unicorns'
                description = 'A collection of small Java helpers'
                url = 'https://github.com/leonbur/unicorns'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'leonbur'
                        name = 'Leon Burdinov'
                        email = 'leon@burdinov.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:burdinov.com/unicorns.git'
                    developerConnection = 'scm:git:git@github.com:burdinov.com/unicorns.git'
                    url = 'https://github.com/leonbur/unicorns'
                }
            }
        }
    }
}


signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
