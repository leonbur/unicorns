plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.nbaztec.coveralls-jacoco' version '1.2.14'
    id "com.vanniktech.maven.publish" version "0.33.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation('org.assertj:assertj-core:3.22.0')
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}


//Publishing

//https://vanniktech.github.io/gradle-maven-publish-plugin/central/#configuring-the-pom
mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    coordinates("com.burdinov", "unicorns", "1.1")

    pom {
        name = "Unicorns"
        description = " Useful Java functions"
        inceptionYear = "2020"
        url = "https://github.com/leonbur/unicorns"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = 'leonbur'
                name = 'Leon Burdinov'
                url = 'https://github.com/leonbur/'
            }
        }
        scm {
            connection = 'scm:git:git@github.com:burdinov.com/unicorns.git'
            developerConnection = 'scm:git:git@github.com:burdinov.com/unicorns.git'
            url = 'https://github.com/leonbur/unicorns'
        }
    }
}

//necessary boilerplate for publishing
afterEvaluate {
    tasks.named("generateMetadataFileForMavenPublication") {
        dependsOn(tasks.named("plainJavadocJar"))
    }
}

